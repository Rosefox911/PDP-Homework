;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-lambda-reader.ss" "lang")((modname 06-2-pretty) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f ())))
(require 2htdp/universe)
(require 2htdp/image)
(require rackunit)
(require rackunit/text-ui)
(require "extras.rkt")

(provide
 expr-width
 expr-to-strings
 display-expr
 SUM-EXP-1
 SUM-EXP-2
 SUM-EXP-3
 SUM-EXP-4
 hw-example-2)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; DATA DEFINITIONS


(define-struct sum-exp (exprs))
;; a Sum-Exp is a (make-sum-exp LoExpr)
;; exprs is a LOExpr which represent a list of operand for addition

;; TEMPLATE
;; sum-exp-fn : Sum-Exp -> ??
;(define (sum-exp-fn s)
;  (...
;   (sum-exp-exprs s)))

(define-struct mult-exp (exprs))
;; a Mult-Exp is a (make-mult-exp LoExpr)
;; exprs is a LOExpr which represent a list of operands for multiplication

;; TEMPLATE
;; mult-exp-fn : Mult-Exp -> ??
;(define (mult-exp-fn m)
;  (...
;   (mult-exp-exprs m)))

;; A Form is one of
;; -- (make-sum-exp NELOExpr) (interp: a sum-exp which represents
;; a summation form)
;; -- (make-mult-exp NELOExpr) (interp: a mult-exp which reprenents
;; a multiplication form)

;; An Expr is one of
;; -- Integer (interp: an integer)
;; -- Form (interp: a form
;; -- (make-sum-exp NELOExpr) (interp: a Sum-Exp which represents 
;; a sum operation)
;; -- (make-mult-exp NELOExpr) (interp: a Mult-Exp which represents
;; a multiplication operation)
;; Interpretation: a sum-exp represents a sum and a mult-exp
;; represents a multiplication. 

;; Expr-fn : Expr -> ??
;(define (expr-fn expr)
;  (cond
;    [(integer? expr) ...]
;    [(sum-exp? expr)
;     (...
;      (loexpr-fn (sum-exp-exprs expr)))]
;    [(mult-exp? expr) 
;     (...
;      (loexpr-fn (mult-exp-exprs expr)))]))

;; A LOExpr is one of
;; -- empty (interp: no exprs in the list)
;; -- (cons Expr LOExpr) (interp: there are exprs within it)

;; loexpr-fn : LoExpr -> ??
;(define (loexpr-fn exprs)
;  (cond
;    [(empty? exprs) ...]
;    [(cons? exprs)
;     (...
;      (expr-fn (first exprs))
;      (loexpr-fn (rest exprs)))]))

;; A NELOExpr is a non-empty LOExpr.

;; END OF DATA DEFINITIONS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; CONSTANTS

(define SPACE " ")
;; just a space character

(define OPEN-PAR "(")
;; left parenthesis

(define CLOSE-PAR ")")
;; right parenthesis

(define SUM-OPERATOR "+")
;; the sum operator

(define SUM-HEADER (string-append OPEN-PAR SUM-OPERATOR))

(define MULT-OPERATOR "*")
;; the multiplication operator

(define MULT-HEADER (string-append OPEN-PAR MULT-OPERATOR))

;;; Assumes that MULT-HEADER and SUM-HEADER are the same size
(define INDENTATION (string-length MULT-HEADER))

;; CONSTANTS FOR TESTING

(define SUM-EXP-1 (make-sum-exp (list 22 333 44)))

(define SUM-EXP-2 (make-sum-exp (list 964 2145 49821)))

(define MULT-EXP-1 (make-mult-exp (list 2 56 3450)))

(define MULT-EXP-2 (make-mult-exp (list 911 8425 0)))

(define SUM-EXP-3 (make-sum-exp (list SUM-EXP-1 MULT-EXP-1)))

(define MULT-EXP-3 (make-mult-exp (list MULT-EXP-2 SUM-EXP-2)))

(define SUM-EXP-4 (make-sum-exp (list SUM-EXP-3 MULT-EXP-3)))

(define hw-example-2
  (make-sum-exp (list (make-mult-exp (list (make-mult-exp (list 1))
                                           (make-sum-exp (list 22 23))))
                      (make-mult-exp
                       (list (make-sum-exp (list 66 67 68))
                             (make-mult-exp (list 42 43 44 45 46 47 48))))
                      (make-mult-exp (list 77 88)))))

;; END OF CONSTANTS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; expr-to-strings : Expr Nat -> ListOf<String>
;; GIVEN: An expression and a width
;; RETURNS: A representation of the expression as a sequence of lines, with
;; each line represented as a string of length not greater than the width.
;; EXAMPLES: TODO
;; STRATEGY: STRUCTURAL DECOMPOSITION on expr : Expr
;; (define (pprint-expr prefix suffix expr width)
;;   (cond
;;     [(integer? expr) (pprint-int prefix suffix expr width)]
;;     [(sum-exp? expr) (pprint-form prefix suffix expr width)]
;;     [(mult-exp? expr) (pprint-form prefix suffix expr width)]))

(define (place-in context string)
  (string-append (context-prefix context)
                 string
                 (context-suffix context)))

(define (pprint-int int context)
  (place-in context (number->string int)))

(define (form-operands form)
  (cond
   [(sum-exp? form) (sum-exp-exprs form)]
   [(mult-exp? form) (mult-exp-exprs form)]))

(define (form-header expr)
  (cond [(sum-exp? expr) SUM-HEADER]
        [(mult-exp? expr) MULT-HEADER]))

(define (form? expr) (or (sum-exp? expr) (mult-exp? expr)))

(define (expr-width expr)
  (cond
    [(integer? expr) (string-length (number->string expr))]
    [(form? expr) (foldr +
                         ;; header and closing parenthesis
                         (+ 1 (string-length (form-header expr)))
                         (map (lambda (exp)
                                ;; spaces between operands
                                (+ 1
                                   ;; width of each operand
                                   (expr-width exp)))
                              (form-operands expr)))]))

(define-struct context (prefix suffix width))

(define (fits-one-line? expr context)
  (>= (context-width context)
      (+ (string-length (context-prefix context))
         (expr-width expr)
         (string-length (context-suffix context)))))

(define (pprint-expr-helper expr context)
  (if (fits-one-line? expr context)
      (pprint-one-line expr context)
      (pprint-multiple expr context)))

(define (prefix-with context prefix)
  (make-context prefix (context-suffix context) (context-width context)))

(define (suffix-with context suffix)
  (make-context (context-prefix context) suffix (context-width context)))

(define (suffix-free context)
  (suffix-with context ""))

(define (prefix-append context addition)
  (prefix-with context (string-append (context-prefix context) addition)))

(define (suffix-append context)
  (suffix-with context (string-append (context-suffix context) CLOSE-PAR)))

(define (indent context header)
  (prefix-with context
               (make-string (+ (string-length header)
                               (string-length (context-prefix context)))
                            #\Space)))

(define (pprint-one-line-wrapper expr context)
  (pprint-one-line-helper expr (prefix-append context SPACE)))

(define (pprint-one-line-helper expr context)
  (cond
   [(integer? expr) (pprint-int expr context)]
   [(form? expr)
    (place-in (suffix-append (foldl (lambda (exp context)
                                      (prefix-with context
                                                   (pprint-one-line-wrapper exp context)))
                                    (prefix-append (suffix-free context)
                                                   (form-header expr))
                                    (form-operands expr)))
              "")]))

(define (pprint-one-line expr context)
  (list (pprint-one-line-helper expr context)))

(define (pprint-expr expr context)
  (pprint-expr-helper expr (prefix-append context SPACE)))

(define (pprint-multiple expr context)
  (cond
   [(integer? expr) (error "not enough room")]
   [(form? expr)
    (append (pprint-expr (first (form-operands expr))
                         (prefix-append (suffix-free context) (form-header expr)))
            (foldr (lambda (exp base)
                     (if (empty? base)  ; last element gets special treatment
                         (pprint-expr exp (suffix-append (indent context (form-header expr))))
                         (append (pprint-expr exp (suffix-free (indent context (form-header expr))))
                                 base)))
                   empty
                   (rest (form-operands expr))))]))

(define (initial-context width)
  (make-context "" "" width))

(define (expr-to-strings expr width)
  (pprint-expr-helper expr (initial-context width)))

(define (display-expr expr n)
    (display-strings! (expr-to-strings expr n)))
